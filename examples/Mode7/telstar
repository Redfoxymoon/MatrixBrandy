   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command.
   70:
   80VER$="Viewdata client version 20210130"
   90:
  100sdl%=0
  110IF (INKEY(-256) AND &A0) = &A0 THEN GOTO 140: REM RISC OS, let's hope the user knows what they're doing.
  120IF INKEY(-256) <> &4D THEN PRINT "This program works on Matrix Brandy for RISC OS or SDL only.":END
  130sdl%=1
  140:
  150SYS"OS_Byte",229,1: SYS "OS_Byte",4,1
  160OSCLI"Key1|_":OSCLI"KEY2"+CHR$(130)
  170IF sdl% THEN *WinTitle Matrix Network Viewdata/Teletext Client
  180ON ERROR GOTO 520
  190MODE 7:OFF: VDU23,16,1|23,18,1|
  200reveal%=0:scan%=0:width%=1:black%=0
  210dh%=0:dhline%=0:borked%=0:telnet%=1:extended%=0
  220allowfs%=1:DIM scr%% 1000
  230REM Variables for downloading
  240screen%=0
  250download%=0
  260X%=OPENUP(FNwelcome)
  270REM Send Telnet init data if required
  280IF telnet%=1 THEN BPUT#X%,&FF,&FD,&03 ELSE VDU23,18,1,2|:BPUT#X%,13
  290REPEAT
  300M%=BGET#X%: IF M%=-2 THEN GOTO 510
  310IF M% >= 0 THEN PROCvdu(M%):GOTO510
  320SYS "OS_Byte",21,0:I%=INKEY(20):IF I%=-1 THEN PROCdhscan: GOTO 510
  330IF I%=35 OR I%=13 THEN I%=95:GOTO500
  340IF I%>=32 AND I%<=126THENGOTO500
  350IF I%=127 THEN I%=8:GOTO410
  360IF I%=4 THEN GOTO 520
  370IF I%=6 THEN PROCtogglescreen: GOTO 300
  380IF I%=18 THEN PROCreveal: GOTO 300
  390IF I%=2 THEN PROCtoggleblack: GOTO 300
  400IF I%=24 THEN GOTO 540
  410IF I%=8 AND borked%=1 THEN VDU8,32:GOTO500
  420IF I%>=136 AND I%<=139 THEN I%-=128
  430IF I%=31THENPROChelp:GOTO300
  440IF I%=130THENI%=FNcontrolcode:GOTO500
  450IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO500
  460IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO500
  470IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO500
  480IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO500
  490IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  500IF I% > 0 THEN BPUT#X%,I%
  510UNTIL M%=-2
  520CLOSE#X%
  530PRINTTAB(12,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  540CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  550:
  560DEFPROCvdu(M%)
  570IF M% >= 32 THEN PROCactvdu: ENDPROC
  580CASE M% OF
  590WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  600WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  610WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  620WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  630WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  640WHEN 12, 13, 30: VDU M%
  650WHEN 17: ON
  660WHEN 20: OFF
  670ENDCASE
  680IF VPOS=24 THEN VDU31,POS,0
  690ENDPROC
  700:
  710DEFPROCcheckdh
  720IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  730ENDPROC
  740:
  750DEFPROCactvdu
  760IFdhline%<>VPOS THEN dh%=0
  770VDU (M% OR 128):IF VPOS=24 OR (VPOS=23 AND POS=40) THEN VDU30
  780ENDPROC
  790:
  800DEFPROCcheckline
  810IF dh%=0 THEN ENDPROC
  820N%=VPOS:PROCcopyline
  830ct%=0:REPEAT:dm%=BGET#X%:IFdm%<>27THENct%+=1
  840UNTIL ct%=40 OR dm%=10 OR dm%=11
  850IF dm%=11 THEN VDU11 ELSE VDU10
  860ENDPROC
  870:
  880DEFPROCdhscan
  890IFscan%=0ENDPROC
  900FOR N%=0 TO 22: FOR P%=0 TO 39
  910IF GET(P%,N%) = 141 THEN PROCcopyline: N%+=1
  920NEXT P%, N%
  930scan%=0
  940ENDPROC
  950:
  960DEFPROCcopyline
  970IF N% > 22 THEN ENDPROC
  980x%=POS:y%=VPOS
  990VDU31,0,N%+1
 1000FOR P%=0 TO 39: VDU GET(P%,N%): NEXT
 1010VDU31,x%,y%
 1020ENDPROC
 1030:
 1040DEFPROCreveal
 1050reveal% EOR=1
 1060VDU23,18,2,reveal%|
 1070ENDPROC
 1080:
 1090DEFPROCtoggleblack
 1100black% EOR=1
 1110VDU23,18,3,black%|
 1120ENDPROC
 1130:
 1140DEFPROCtogglescreen
 1150IF allowfs%=0THENENDPROC
 1160OSCLI"Fullscreen"
 1170SYS"OS_Byte",42 TO ,fs%
 1180IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1190ENDPROC
 1200:
 1210DEFFNwelcome
 1220PROCwelcomescreen
 1230REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 55) OR A%=6 OR A%=24 OR A%=84 OR A%=116
 1240IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1250IF A% = 6 THEN PROCtogglescreen: GOTO 1230
 1260IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1270IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1280IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1290IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1300IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1310IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1320IF A% = 55 THEN borked%=0: S$ = "alterant.leenooks.net:516"
 1330IF A% = 84 OR A% = 116 THEN telnet%=telnet% EOR 1: GOTO 1220
 1340CLS
 1350="ip0:"+S$
 1360:
 1370DEFPROCwelcomescreen:CLS
 1380PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1390PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1400PRINT
 1410PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1420PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1430PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1440PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1450PRINT CHR$(131);"5 - NXtel"
 1460PRINT CHR$(131);"6 - TEEFAX";CHR$(133);"and Matrix Teletext"
 1470PRINT CHR$(131);"7 - ANSItex BBS"
 1480
 1490PRINT
 1500PRINT CHR$(131);"T - Toggle Telnet codes -";
 1510IF telnet%=1 THEN PRINT CHR$(130);"ON" ELSE PRINT CHR$(129);"OFF"
 1520PRINT
 1530PRINT CHR$(129);"0 - Exit"
 1540PRINT TAB(0,15)CHR$(134);"While the session is running:"
 1550IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1560PRINT CHR$(134);" Press CTRL-R to toggle Reveal"
 1570PRINT CHR$(134);" Press CTRL-B to toggle Black"
 1580PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1590PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1600PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1610PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1620PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1630ENDPROC
 1640:
 1650DEFPROChelp
 1660x%=POS:y%=VPOS:PROCstorescreen:CLS
 1670PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1680PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1690PRINT
 1700PRINT CHR$(134);"Special keys available:":PRINT
 1710IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1720PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1730PRINT CHR$(134);" Press";CHR$(131);"CTRL-B";CHR$(134);"to toggle Black";CHR$(133);"*"
 1740PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1750PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1760PRINT
 1770PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 1780PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 1790PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 1800PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 1810PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 1820PRINT
 1830PRINT CHR$(130);" F2: Send teletext control code":PRINT
 1840PRINT CHR$(133);"*";CHR$(134);"This also switches the Hold Character"
 1850PRINT CHR$(134);"behaviour between that of the";CHR$(131);"SAA5050";CHR$(134);"/"
 1860PRINT CHR$(131);"BBC Micro";CHR$(134);"and later Teletext/Videotex"
 1870PRINT CHR$(134);"specifications."
 1880IFGET
 1890PROCrestorescreen
 1900VDU31,x%,y%
 1910ENDPROC
 1920:
 1930DEFFNcontrolcode
 1940x%=POS:y%=VPOS:PROCstorescreen:CLS
 1950VDU23,18,3,1|
 1960PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1970PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1980PRINT
 1990PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2000PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2010PRINT " @:(text black)     P:(graphics black)"
 2020PRINT CHR$(129);"A: text red        Q: graphics red"
 2030PRINT CHR$(130);"B: text green      R: graphics green"
 2040PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 2050PRINT CHR$(132);"D: text blue       T: graphics blue"
 2060PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 2070PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 2080PRINT CHR$(135);"G: text white      W: graphics white"
 2090PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 2100PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 2110PRINT " J: &8A (End Box)   Z: Separated gfx"
 2120PRINT " K: &8B (Start Box)";CHR$&9B;"[: &9B (Esc/Switch)"
 2130PRINT " L: Dbl Height Off ";CHR$&9B;"|: Black background"
 2140PRINT " M: Dbl Height On  ";CHR$&9B;"]: Set bg colour"
 2150PRINT " N: &8E (dbl width)";CHR$&9B;"^: Hold graphics"
 2160PRINT " O: &8F (dbl size) ";CHR$&9B;"#: Release graphics"
 2170PRINT
 2180PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2190PRINT CHR$(134);"have no effect in this client."
 2200t%=GET
 2210SYS6,25,16
 2220PROCrestorescreen
 2230VDU31,x%,y%,23,18,3,0|
 2240=(t% AND 31)+128
 2250:
 2260DEFPROCstorescreen
 2270FOR p%=0 TO 999
 2280p%?scr%%=GET(p% MOD 40,p% DIV 40)
 2290NEXT
 2300ENDPROC
 2310:
 2320DEFPROCrestorescreen
 2330VDU30: FOR p%=0 TO 999: VDU (p%?scr%%) OR &80: NEXT
 2340ENDPROC
