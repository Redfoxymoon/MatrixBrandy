   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command.
   70:
   80VER$="Viewdata client version 20210403"
   90:
  100sdl%=0
  110IF (INKEY(-256) AND &A0) = &A0 THEN GOTO 140: REM RISC OS, let's hope the user knows what they're doing.
  120IF INKEY(-256) <> &4D THEN PRINT "This program works on Matrix Brandy for RISC OS or SDL only.":END
  130sdl%=1
  140:
  150SYS"OS_Byte",229,1: SYS "OS_Byte",4,1
  160OSCLI"Key1|_":OSCLI"KEY2"+CHR$(130)
  170ON ERROR GOTO 550
  180MODE 7:OFF: VDU23,16,1|23,18,1|
  190reveal%=0:scan%=0:width%=1:black%=0:ost%=0
  200dh%=0:dhline%=0:borked%=0:telnet%=1:extended%=0
  210allowfs%=1:DIM scr%% 1000
  220REM Variables for downloading
  230screen%=0
  240download%=0
  250X%=OPENUP("ip0:"+FNwelcome):VDU28,0,23,39,0,31
  260PROCstatusbar(CHR$132+"* "+S$)
  270IF sdl% THEN OSCLI"WinTitle Matrix Network Viewdata/Teletext Client: Connected"
  280REM Send Telnet init data if required
  290IF telnet%=1 THEN BPUT#X%,&FF,&FD,&03 ELSE VDU23,18,1,2|:BPUT#X%,13
  300REPEAT
  310M%=BGET#X%: IF M%=-2 THEN GOTO 540
  320IF M% = -1 AND TIME > ost%+5 THEN PROCstatusxfer(0)
  330IF M% >= 0 THEN PROCvdu(M%):PROCstatusxfer(1): GOTO540
  340SYS "OS_Byte",21,0:I%=INKEY(20):IF I%=-1 THEN PROCdhscan: GOTO 540
  350PROCstatusxfer(2)
  360IF I%=35 OR I%=13 THEN I%=95:GOTO530
  370IF I%>=32 AND I%<=126THENGOTO530
  380IF I%=127 THEN I%=8:GOTO440
  390IF I%=4 THEN GOTO 550
  400IF I%=6 THEN PROCtogglescreen: GOTO 310
  410IF I%=18 THEN PROCreveal: GOTO 310
  420IF I%=2 THEN PROCtoggleblack: GOTO 310
  430IF I%=24 THEN GOTO 580
  440IF I%=8 AND borked%=1 THEN VDU8,32:GOTO530
  450IF I%>=136 AND I%<=139 THEN I%-=128
  460IF I%=31THENPROChelp:GOTO310
  470IF I%=130THENI%=FNcontrolcode:GOTO530
  480IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO530
  490IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO530
  500IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO530
  510IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO530
  520IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  530IF I% > 0 THEN BPUT#X%,I%
  540UNTIL M%=-2
  550CLOSE#X%
  560IF sdl% THEN PROCstatusbar(CHR$129+"Disconnected  "+CHR$156): *WinTitle Matrix Network Viewdata/Teletext Client: Disconnected
  570VDU26:PRINTTAB(18,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  580CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  590:
  600DEFPROCvdu(M%)
  610IF M% >= 32 THEN PROCactvdu: ENDPROC
  620CASE M% OF
  630WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  640WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  650WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  660WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  670WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  680WHEN 12, 13, 30: VDU M%
  690WHEN 17: ON
  700WHEN 20: OFF
  710ENDCASE
  720IF VPOS=24 THEN VDU31,POS,0
  730ENDPROC
  740:
  750DEFPROCcheckdh
  760IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  770ENDPROC
  780:
  790DEFPROCactvdu
  800IFdhline%<>VPOS THEN dh%=0
  810VDU (M% OR 128):IF VPOS=24 OR (VPOS=23 AND POS=40) THEN VDU30
  820ENDPROC
  830:
  840DEFPROCcheckline
  850IF dh%=0 THEN ENDPROC
  860N%=VPOS:PROCcopyline
  870ct%=0:REPEAT:dm%=BGET#X%:IFdm%<>27THENct%+=1
  880UNTIL ct%=40 OR dm%=10 OR dm%=11
  890IF dm%=11 THEN VDU11 ELSE VDU10
  900ENDPROC
  910:
  920DEFPROCdhscan
  930IFscan%=0ENDPROC
  940FOR N%=0 TO 22: FOR P%=0 TO 39
  950IF GET(P%,N%) = 141 THEN PROCcopyline: N%+=1
  960NEXT P%, N%
  970scan%=0
  980ENDPROC
  990:
 1000DEFPROCcopyline
 1010IF N% > 22 THEN ENDPROC
 1020x%=POS:y%=VPOS
 1030VDU31,0,N%+1
 1040FOR P%=0 TO 39: VDU GET(P%,N%): NEXT
 1050VDU31,x%,y%
 1060ENDPROC
 1070:
 1080DEFPROCreveal
 1090reveal% EOR=1
 1100VDU23,18,2,reveal%|
 1110ENDPROC
 1120:
 1130DEFPROCtoggleblack
 1140black% EOR=1
 1150VDU23,18,3,black%|
 1160ENDPROC
 1170:
 1180DEFPROCtogglescreen
 1190IF allowfs%=0THENENDPROC
 1200OSCLI"Fullscreen"
 1210SYS"OS_Byte",42 TO ,fs%
 1220IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1230ENDPROC
 1240:
 1250DEFPROCstatusbar(status$)
 1260IFsdl%=0 THEN ENDPROC
 1270$&7FC0=STRING$(40," ")
 1280status$=CHR$157+LEFT$(status$, 37)
 1290$&7FC0=status$: ?(&7FC0+LENstatus$)=32
 1300ENDPROC
 1310:
 1320DEFPROCstatusxfer(st%)
 1330IFsdl%=0 THEN ENDPROC
 1340CASE st% OF
 1350WHEN 1:?&7FC2=ASC"["
 1360WHEN 2:?&7FC2=ASC"]"
 1370OTHERWISE: ?&7FC2=32
 1380ENDCASE
 1390ost%=TIME
 1400ENDPROC
 1410:
 1420DEFFNwelcome
 1430PROCwelcomescreen
 1440REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 55) OR A%=6 OR A%=24 OR A%=84 OR A%=116
 1450IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1460IF A% = 6 THEN PROCtogglescreen: GOTO 1440
 1470IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1480IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1490IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1500IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1510IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1520IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1530IF A% = 55 THEN borked%=0: S$ = "alterant.leenooks.net:516"
 1540IF A% = 84 OR A% = 116 THEN telnet%=telnet% EOR 1: GOTO 1430
 1550CLS
 1560=S$
 1570:
 1580DEFPROCwelcomescreen:CLS:VDU26
 1590IF sdl% THEN *WinTitle Matrix Network Viewdata/Teletext Client
 1600PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1610PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1620PRINT
 1630PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1640PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1650PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1660PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1670PRINT CHR$(131);"5 - NXtel"
 1680PRINT CHR$(131);"6 - TEEFAX";CHR$(133);"and Matrix Teletext"
 1690PRINT CHR$(131);"7 - ANSItex BBS"
 1700
 1710PRINT
 1720PRINT CHR$(131);"T - Toggle Telnet codes -";
 1730IF telnet%=1 THEN PRINT CHR$(130);"ON" ELSE PRINT CHR$(129);"OFF"
 1740PRINT
 1750PRINT CHR$(129);"0 - Exit"
 1760PRINT TAB(0,15)CHR$(134);"While the session is running:"
 1770IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1780PRINT CHR$(134);" Press CTRL-R to toggle Reveal"
 1790PRINT CHR$(134);" Press CTRL-B to toggle Black"
 1800PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1810PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1820PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1830PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1840PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1850ENDPROC
 1860:
 1870DEFPROChelp
 1880x%=POS:y%=VPOS:VDU26:PROCstorescreen:CLS
 1890PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1900PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1910PRINT
 1920PRINT CHR$(134);"Special keys available:":PRINT
 1930IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1940PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1950PRINT CHR$(134);" Press";CHR$(131);"CTRL-B";CHR$(134);"to toggle Black";CHR$(133);"*"
 1960PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1970PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1980PRINT
 1990PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 2000PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 2010PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 2020PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 2030PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 2040PRINT
 2050PRINT CHR$(130);" F2: Send teletext control code":PRINT
 2060PRINT CHR$(133);"*";CHR$(134);"This also switches the Hold Character"
 2070PRINT CHR$(134);"behaviour between that of the";CHR$(131);"SAA5050";CHR$(134);"/"
 2080PRINT CHR$(131);"BBC Micro";CHR$(134);"and later Teletext/Videotex"
 2090PRINT CHR$(134);"specifications."
 2100IFGET
 2110PROCrestorescreen
 2120VDU28,0,23,39,0,31,x%,y%
 2130ENDPROC
 2140:
 2150DEFFNcontrolcode
 2160x%=POS:y%=VPOS:VDU26:PROCstorescreen:CLS
 2170VDU23,18,3,1|
 2180PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 2190PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 2200PRINT
 2210PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2220PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2230PRINT " @:(text black)     P:(graphics black)"
 2240PRINT CHR$(129);"A: text red        Q: graphics red"
 2250PRINT CHR$(130);"B: text green      R: graphics green"
 2260PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 2270PRINT CHR$(132);"D: text blue       T: graphics blue"
 2280PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 2290PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 2300PRINT CHR$(135);"G: text white      W: graphics white"
 2310PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 2320PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 2330PRINT " J: &8A (End Box)   Z: Separated gfx"
 2340PRINT " K: &8B (Start Box)";CHR$&9B;"[: &9B (Esc/Switch)"
 2350PRINT " L: Dbl Height Off ";CHR$&9B;"|: Black background"
 2360PRINT " M: Dbl Height On  ";CHR$&9B;"]: Set bg colour"
 2370PRINT " N: &8E (dbl width)";CHR$&9B;"^: Hold graphics"
 2380PRINT " O: &8F (dbl size) ";CHR$&9B;"#: Release graphics"
 2390PRINT
 2400PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2410PRINT CHR$(134);"have no effect in this client."
 2420t%=GET
 2430SYS6,25,16
 2440PROCrestorescreen
 2450VDU28,0,23,39,0,31,x%,y%,23,18,3,0|
 2460=(t% AND 31)+128
 2470:
 2480DEFPROCstorescreen
 2490FOR p%=0 TO 999
 2500p%?scr%%=GET(p% MOD 40,p% DIV 40)
 2510NEXT
 2520ENDPROC
 2530:
 2540DEFPROCrestorescreen
 2550VDU30: FOR p%=0 TO 999: VDU (p%?scr%%) OR &80: NEXT
 2560ENDPROC
