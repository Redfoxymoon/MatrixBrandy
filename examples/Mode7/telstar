   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command.
   70:
   80VER$="Viewdata client version 20220520"
   90:
  100IF INKEY(-256) <> &4D THEN PRINT "This program works on Matrix Brandy for RISC OS or SDL only.":END
  110SYS"Brandy_Platform" TO ,,,sdl%,,leg%
  120IF sdl% = 0 AND ((leg% AND &F0) <> &A0) THEN PRINT "This program works on Matrix Brandy for RISC OS or SDL only.":END
  130:
  140SYS"OS_Byte",229,1: SYS "OS_Byte",4,1
  150OSCLI"Key1|_":OSCLI"KEY2"+CHR$(130)
  160ON ERROR GOTO 560
  170MODE 7:OFF: VDU23,16,1|23,18,1|
  180reveal%=0:scan%=0:width%=1:black%=0:ost%=0
  190borked%=0:telnet%=1:extended%=0
  200allowfs%=1:DIM scr%% 1000
  210REM Variables for downloading
  220screen%=0
  230download%=0
  240X%=OPENUP(FNwelcome):VDU28,0,23,39,0,31
  250S$=MID$(S$,1+INSTR(S$,":"))
  260PROCstatusbar(CHR$132+"* "+S$)
  270IF sdl% THEN OSCLI"WinTitle Matrix Network Viewdata/Teletext Client: Connected"
  280REM Send Telnet init data if required
  290IF telnet%=1 THEN BPUT#X%,&FF,&FD,&03 ELSE VDU23,18,1,2|:BPUT#X%,13
  300REPEAT
  310M%=BGET#X%: IF M%=-2 THEN GOTO 550
  320IF M% = -1 AND TIME > ost%+5 THEN PROCstatusxfer(0)
  330IF M% = -1 THEN PROCdhscan
  340IF M% >= 0 THEN PROCvdu(M%):PROCstatusxfer(1): GOTO550
  350SYS "OS_Byte",21,0:I%=INKEY(20):IF I%=-1 THEN scan%=1:PROCdhscan: GOTO 550
  360PROCstatusxfer(2)
  370IF I%=35 OR I%=13 THEN I%=95:GOTO540
  380IF I%>=32 AND I%<=126THENGOTO540
  390IF I%=127 THEN I%=8:GOTO450
  400IF I%=4 THEN GOTO 560
  410IF I%=6 THEN PROCtogglescreen: GOTO 310
  420IF I%=18 THEN PROCreveal: GOTO 310
  430IF I%=2 THEN PROCtoggleblack: GOTO 310
  440IF I%=24 THEN GOTO 590
  450IF I%=8 AND borked%=1 THEN VDU8,32:GOTO540
  460IF I%>=136 AND I%<=139 THEN I%-=128
  470IF I%=31THENPROChelp:GOTO310
  480IF I%=130THENI%=FNcontrolcode:GOTO540
  490IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO540
  500IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO540
  510IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO540
  520IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO540
  530IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  540IF I% > 0 THEN BPUT#X%,I%
  550UNTIL M%=-2
  560CLOSE#X%
  570IF sdl% THEN PROCstatusbar(CHR$129+"Disconnected  "+CHR$156): *WinTitle Matrix Network Viewdata/Teletext Client: Disconnected
  580VDU26:PRINTTAB(18,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  590CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  600:
  610DEFPROCvdu(M%)
  620IF M% >= 32 THEN PROCactvdu: ENDPROC
  630CASE M% OF
  640WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  650WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  660WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  670WHEN 10: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  680WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  690WHEN 12, 13, 30: VDU M%
  700WHEN 17: ON
  710WHEN 20: OFF
  720ENDCASE
  730IF VPOS=24 THEN VDU31,POS,0
  740ENDPROC
  750:
  760DEFPROCcheckdh
  770IFN%=141THENscan%=1
  780ENDPROC
  790:
  800DEFPROCactvdu
  810VDU (M% OR 128):IF VPOS=24 OR (VPOS=23 AND POS=40) THEN VDU30
  820ENDPROC
  830:
  840DEFPROCdhscan
  850IFscan%=0ENDPROC
  860FOR N%=0 TO 22: FOR P%=0 TO 39
  870IF GET(P%,N%) = 141 THEN PROCcopyline: N%+=1
  880NEXT P%, N%
  890scan%=0
  900ENDPROC
  910:
  920DEFPROCcopyline
  930IF N% > 22 THEN ENDPROC
  940x%=POS:y%=VPOS
  950VDU31,0,N%+1
  960FOR P%=0 TO 39: VDU GET(P%,N%): NEXT
  970VDU31,x%,y%
  980ENDPROC
  990:
 1000DEFPROCreveal
 1010reveal% EOR=1
 1020VDU23,18,2,reveal%|
 1030ENDPROC
 1040:
 1050DEFPROCtoggleblack
 1060black% EOR=1
 1070VDU23,18,3,black%|
 1080ENDPROC
 1090:
 1100DEFPROCtogglescreen
 1110IF allowfs%=0THENENDPROC
 1120OSCLI"Fullscreen"
 1130SYS"OS_Byte",42 TO ,fs%
 1140IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1150ENDPROC
 1160:
 1170DEFPROCstatusbar(status$)
 1180IFsdl%=0 THEN ENDPROC
 1190$&7FC0=STRING$(40," ")
 1200status$=CHR$157+LEFT$(status$, 37)
 1210$&7FC0=status$: ?(&7FC0+LENstatus$)=32
 1220ENDPROC
 1230:
 1240DEFPROCstatusxfer(st%)
 1250IFsdl%=0 THEN ENDPROC
 1260CASE st% OF
 1270WHEN 1:?&7FC2=ASC"["
 1280WHEN 2:?&7FC2=ASC"]"
 1290OTHERWISE: ?&7FC2=32
 1300ENDCASE
 1310ost%=TIME
 1320ENDPROC
 1330:
 1340DEFFNwelcome
 1350PROCwelcomescreen
 1360REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 56) OR A%=6 OR A%=24 OR A%=84 OR A%=116
 1370IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1380IF A% = 6 THEN PROCtogglescreen: GOTO 1360
 1390IF A% = 49 THEN borked%=1: S$ = "ip0:glasstty.com:6502"
 1400IF A% = 50 THEN borked%=1: S$ = "ip0:glasstty.com:6503"
 1410IF A% = 51 THEN borked%=1: S$ = "ip0:glasstty.com:6504"
 1420IF A% = 52 THEN borked%=0: S$ = "ip0:fish.ccl4.org:23"
 1430IF A% = 53 THEN borked%=0: S$ = "ip0:nx.nxtel.org:23280"
 1440IF A% = 54 THEN borked%=0: S$ = "ip0:teletext.matrixnetwork.co.uk:6502"
 1450IF A% = 55 THEN borked%=0: S$ = "ip0:teletext.matrixnetwork.co.uk:48065"
 1460IF A% = 56 THEN borked%=0: S$ = "ip0:teletext.matrixnetwork.co.uk:31744"
 1470IF A% = 84 OR A% = 116 THEN telnet%=telnet% EOR 1: GOTO 1350
 1480CLS
 1490=S$
 1500:
 1510DEFPROCwelcomescreen:CLS:VDU26
 1520IF sdl% THEN *WinTitle Matrix Network Viewdata/Teletext Client
 1530PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1540PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1550PRINT
 1560PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1570PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1580PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1590PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1600PRINT CHR$(131);"5 - NXtel"
 1610PRINT CHR$(131);"6 - TEEFAX";CHR$(133);"and Matrix Teletext"
 1620PRINT CHR$(131);"7 - CEEFAX";CHR$(133);"(NMS - teletext)"
 1630PRINT CHR$(131);"8 - EMFFAX";CHR$(133);"(teletext)"
 1640
 1650PRINT
 1660PRINT CHR$(131);"T - Toggle Telnet codes -";
 1670IF telnet%=1 THEN PRINT CHR$(130);"ON" ELSE PRINT CHR$(129);"OFF"
 1680PRINT CHR$(129);"0 - Exit"
 1690PRINT TAB(0,15)CHR$(134);"While the session is running:"
 1700IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1710PRINT CHR$(134);" Press CTRL-R to toggle Reveal"
 1720PRINT CHR$(134);" Press CTRL-B to toggle Black"
 1730PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1740PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1750PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1760PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1770PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1780ENDPROC
 1790:
 1800DEFPROChelp
 1810x%=POS:y%=VPOS:VDU26:PROCstorescreen:CLS
 1820PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1830PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1840PRINT
 1850PRINT CHR$(134);"Special keys available:":PRINT
 1860IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1870PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1880PRINT CHR$(134);" Press";CHR$(131);"CTRL-B";CHR$(134);"to toggle Black";CHR$(133);"*"
 1890PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1900PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1910PRINT
 1920PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 1930PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 1940PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 1950PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 1960PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 1970PRINT
 1980PRINT CHR$(130);" F2: Send teletext control code":PRINT
 1990PRINT CHR$(133);"*";CHR$(134);"This also switches the Hold Character"
 2000PRINT CHR$(134);"behaviour between that of the";CHR$(131);"SAA5050";CHR$(134);"/"
 2010PRINT CHR$(131);"BBC Micro";CHR$(134);"and later Teletext/Videotex"
 2020PRINT CHR$(134);"specifications."
 2030IFGET
 2040PROCrestorescreen
 2050VDU28,0,23,39,0,31,x%,y%
 2060ENDPROC
 2070:
 2080DEFFNcontrolcode
 2090x%=POS:y%=VPOS:VDU26:PROCstorescreen:CLS
 2100VDU23,18,3,1|
 2110PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 2120PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 2130PRINT
 2140PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2150PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2160PRINT " @:(text black)     P:(graphics black)"
 2170PRINT CHR$(129);"A: text red        Q: graphics red"
 2180PRINT CHR$(130);"B: text green      R: graphics green"
 2190PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 2200PRINT CHR$(132);"D: text blue       T: graphics blue"
 2210PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 2220PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 2230PRINT CHR$(135);"G: text white      W: graphics white"
 2240PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 2250PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 2260PRINT " J: &8A (End Box)   Z: Separated gfx"
 2270PRINT " K: &8B (Start Box)";CHR$&9B;"[: &9B (Esc/Switch)"
 2280PRINT " L: Dbl Height Off ";CHR$&9B;"|: Black background"
 2290PRINT " M: Dbl Height On  ";CHR$&9B;"]: Set bg colour"
 2300PRINT " N: &8E (dbl width)";CHR$&9B;"^: Hold graphics"
 2310PRINT " O: &8F (dbl size) ";CHR$&9B;"#: Release graphics"
 2320PRINT
 2330PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2340PRINT CHR$(134);"have no effect in this client."
 2350t%=GET
 2360SYS6,25,16
 2370PROCrestorescreen
 2380VDU28,0,23,39,0,31,x%,y%,23,18,3,0|
 2390=(t% AND 31)+128
 2400:
 2410DEFPROCstorescreen
 2420FOR p%=0 TO 999
 2430p%?scr%%=GET(p% MOD 40,p% DIV 40)
 2440NEXT
 2450ENDPROC
 2460:
 2470DEFPROCrestorescreen
 2480VDU30: FOR p%=0 TO 999: VDU (p%?scr%%) OR &80: NEXT
 2490ENDPROC
