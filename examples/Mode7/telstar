   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command. The VDU23 commands in PROCdhscan and PROCreveal
   70REM are from RISC OS 5.
   80:
   90*FX229,1
  100*WinTitle Matrix Brandy BASIC Telstar Client
  110REMON ERROR GOTO 320
  120MODE 7:OFF
  130reveal%=0:scan%=0
  140dh%=0:dhline%=0
  150REM Variables for downloading
  160screen%=0
  170download%=0
  180X%=OPENUP FNwelcome
  190BPUT#X%,13
  200REPEAT
  210M%=BGET#X%: IF M%=-2 THEN GOTO 310
  220IF M% >= 0 THEN PROCvdu(M%): GOTO 310
  230PROCdhscan
  240I%=INKEY(10):IF I%=35 OR I%=13 THEN I%=95
  250IF I%=-1 THEN GOTO 310
  260IF I%=6 THEN OSCLI"FullScreen": GOTO 210
  270IF I%=16 THEN PROCdumpscreen: GOTO 210
  280IF I%=18 THEN PROCreveal: GOTO 210
  290IF I%=24 THEN GOTO 320
  295IF I%>139 AND I%<144 THEN I%=I%-132
  300IF I% > 0 THEN BPUT#X%,I%
  310UNTIL M%=-2
  320CLOSE#X%:
  330PRINTTAB(9,24)CHR$(128+RND(7));"Press any key to exit.";
  340A=GET: PRINT: ON
  350END
  360:
  370DEFPROCvdu(M%)
  380IF M% >= 32 THEN PROCactvdu: ENDPROC
  390CASE M% OF
  400WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: VDU (N%+64): PROCcheckdh: ENDPROC
  410WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8,32,8
  420WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  430WHEN 10: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  440WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  450WHEN 12, 13, 30: VDU M%
  460WHEN 17: ON
  470WHEN 20: OFF
  472ENDCASE
  474IF VPOS=24 THEN VDU31,POS,0
  480ENDPROC
  490:
  500DEFPROCcheckdh
  510IFN%=77THEN dh%=1:dhline%=VPOS:scan%=1
  520IFN%=76THEN dh%=0
  530IFN%=77ORN%=76THENVDU8,10,N%+64,11
  540ENDPROC
  550:
  560DEFPROCactvdu
  570IFdhline%<>VPOS THEN dh%=0
  580VDU (M% OR 128)
  590IFdh%=1 THEN VDU8,10,(M% OR 128),11
  600ENDPROC
  610:
  620DEFPROCdhscan
  630IFscan%=0THEN ENDPROC
  640VDU23,18,1,0|
  650FOR N%=0 TO 24: FOR P%=0 TO 39
  660IF ?(&FFFF7C00+(40*N%)+P%) = 141 THEN PROCcopyline: N%=N%+1
  670NEXT P%, N%
  680VDU23,18,1,2|:scan%=0
  690ENDPROC
  700:
  710DEFPROCcopyline
  720FOR P%=0 TO 36 STEP 4
  730!(&FFFF7C00+(40*(N%+1))+P%) = !(&FFFF7C00+(40*N%)+P%)
  740NEXT P%
  750ENDPROC
  760:
  770DEFPROCreveal
  780reveal% = reveal% EOR 1
  790VDU23,18,2,reveal%|
  800ENDPROC
  810:
  820DEFPROCcursor_up
  830IF VPOS > 0 THEN VDU11
  840ENDPROC
  850:
  860DEFPROCdumpscreen: REM For debug purposes, triggered on CTRL-P
  870F$="vt."+STR$screen%+".header"
  880Q%=OPENOUT F$
  890FOR P%=0 TO 39: B%=?(&FFFF7C00+P%)
  900IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
  910NEXT:CLOSE#Q%
  920F$="vt."+STR$screen%+".body"
  930Q%=OPENOUT F$
  940FOR P%=40 TO 959: B%=?(&FFFF7C00+P%)
  950IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
  960NEXT:CLOSE#Q%:screen%+=1
  970ENDPROC
  980:
 1030DEFFNwelcome
 1040PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156);
 1050PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156);
 1060PRINT
 1070PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1080PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1090PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1100PRINT
 1110PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1120PRINT
 1130PRINT CHR$(129);"0 - Exit"
 1140PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
 1150PRINT CHR$(134);"  Press CTRL-F to toggle Full Screen":
 1160PRINT CHR$(134);"  Press CTRL-R to toggle Reveal":
 1170PRINT CHR$(134);"  Press CTRL-X to exit immediately": PRINT
 1180PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1190REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 52) OR A%=6 OR A%=23 OR A%=24
 1200IF A% = 24 OR A% = 48 THEN ON:END
 1210IF A% = 6 THEN OSCLI"FullScreen": GOTO 1190
 1220IF A% = 49 THEN S$ = "glasstty.com:6502"
 1230IF A% = 50 THEN S$ = "glasstty.com:6503"
 1240IF A% = 51 THEN S$ = "glasstty.com:6504"
 1250IF A% = 52 THEN S$ = "fish.ccl4.org:23"
 1260CLS
 1270="ip0:"+S$
