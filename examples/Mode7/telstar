   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command. The VDU23 commands in PROCdhscan and PROCreveal
   70REM are from RISC OS 5.
   80:
   90*FX229,1
  100*WinTitle Matrix Brandy BASIC Telstar Client
  110ON ERROR GOTO 360
  120MODE 7:OFF: VDU23,16,1,0|23,18,1,0|
  130reveal%=0:scan%=0:width%=1:auth%=0
  140dh%=0:dhline%=0:borked%=0
  150REM Variables for downloading
  160screen%=0
  170download%=0
  180X%=OPENUP(FNwelcome)
  190FOR l%=1TO6: READ v%:BPUT#X%,v%:NEXT
  200REPEAT
  210M%=BGET#X%: IF M%=-2 THEN GOTO 350
  220IF M% >= 0 THEN PROCvdu(M%): GOTO 350
  230I%=INKEY(10):IF I%=-1 THEN PROCdhscan: GOTO 350
  240IF I%=35 OR I%=13 THEN I%=95
  250IF I%=127 THEN I%=8
  260IF I%=1 THEN auth% EOR=1:GOTO 210
  270IF I%=6 THEN PROCtogglescreen: GOTO 210
  280IF I%=16 THEN PROCdumpscreen: GOTO 210
  290IF I%=18 THEN PROCreveal: GOTO 210
  300IF I%=23 THEN PROCwidth: GOTO 210
  310IF I%=24 THEN GOTO 360
  320IF I%=8 AND borked%=1 THEN VDU8,32
  330IF I%>139 AND I%<144 THEN I%-=132
  340IF I% > 0 THEN BPUT#X%,I%
  350UNTIL M%=-2
  360CLOSE#X%
  370PRINTTAB(9,24)CHR$(128+RND(7));"Press any key to exit.";
  380A=GET: PRINT: ON: OSCLI"FX229"
  390END
  400:
  410DEFPROCvdu(M%)
  420PROCauth:IF M% >= 32 THEN PROCactvdu: ENDPROC
  430CASE M% OF
  440WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: PROCauth:VDU N%: PROCcheckdh: ENDPROC
  450WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  460WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  470WHEN 10: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  480WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  490WHEN 12, 13, 30: VDU M%
  500WHEN 17: ON
  510WHEN 20: OFF
  520ENDCASE
  530IF VPOS=24 THEN VDU31,POS,0
  540ENDPROC
  550:
  560DEFPROCcheckdh
  570IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  580IFN%=140THEN dh%=0
  590IFN%=141ORN%=140THENVDU8,10,N%,11
  600ENDPROC
  610:
  620DEFPROCactvdu
  630IFdhline%<>VPOS THEN dh%=0
  640VDU (M% OR 128)
  650IFdh%=1 THEN VDU8,10,(M% OR 128),11
  660ENDPROC
  670:
  680DEFPROCdhscan
  690IFscan%=0ENDPROC
  700FOR N%=0 TO 23: FOR P%=0 TO 39
  710IF ?(&FFFF7C00+(40*N%)+P%) = 141 THEN PROCcopyline: N%+=1
  720NEXT P%, N%
  730scan%=0
  740ENDPROC
  750:
  760DEFPROCcopyline
  770IF N% > 23 THEN ENDPROC
  780FOR P%=0 TO 36 STEP 4
  790!(&FFFF7C00+(40*(N%+1))+P%) = !(&FFFF7C00+(40*N%)+P%)
  800NEXT P%
  810ENDPROC
  820:
  830DEFPROCreveal
  840reveal% EOR=1
  850VDU23,18,2,reveal%|
  860ENDPROC
  870:
  880DEFPROCwidth
  890width% EOR=1
  900VDU23,18,255,12+(width%*4)|
  910ENDPROC
  920:
  930DEFPROCauth
  940IFauth% WAIT1
  950ENDPROC
  960:
  970DEFPROCdumpscreen: REM For debug purposes, triggered on CTRL-P
  980F$="vt."+STR$screen%+".header"
  990Q%=OPENOUT F$
 1000FOR P%=0 TO 39: B%=?(&FFFF7C00+P%)
 1010IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1020NEXT:CLOSE#Q%
 1030F$="vt."+STR$screen%+".body"
 1040Q%=OPENOUT F$
 1050FOR P%=40 TO 959: B%=?(&FFFF7C00+P%)
 1060IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1070NEXT:CLOSE#Q%:screen%+=1
 1080ENDPROC
 1090:
 1100DEFPROCtogglescreen
 1110OSCLI"Fullscreen"
 1120SYS"OS_Byte",42 TO ,fs%
 1130IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1140ENDPROC
 1150:
 1160DEFFNwelcome
 1170PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156)
 1180PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156)
 1190PRINT
 1200PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1210PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1220PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1230PRINT
 1240PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1250PRINT CHR$(131);"5 - NXtel"
 1260PRINT CHR$(131);"6 - TEEFAX - P999 to exit"
 1270PRINT
 1280PRINT CHR$(129);"0 - Exit"
 1290PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
 1300PRINT CHR$(134);" Press CTRL-A to toggle Authentic Mode":
 1310PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1320PRINT CHR$(134);" Press CTRL-R to toggle Reveal":
 1330PRINT CHR$(134);" Press CTRL-X to exit immediately": PRINT
 1340PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1350REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 54) OR A%=1 OR A%=6 OR A%=24
 1360IF A% = 23 THEN PROCwidth:GOTO 1350
 1370IF A% = 24 OR A% = 48 THEN ON:OSCLI"FX229":END
 1380IF A% = 1 THEN auth% EOR=1: GOTO 1350
 1390IF A% = 6 THEN PROCtogglescreen: GOTO 1350
 1400IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1410IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1420IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1430IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1440IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1450IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1460CLS
 1470="ip0:"+S$
 1480REM Telnet init data
 1490DATA&FF,&FD,&03,&FF,&FD,&01
