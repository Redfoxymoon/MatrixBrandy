   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command. The VDU23 commands in PROCdhscan and PROCreveal
   70REM are from RISC OS 5.
   80:
   90*FX229,1
  100*WinTitle Matrix Brandy BASIC Telstar Client
  110ON ERROR GOTO 350
  120MODE 7:OFF: VDU23,16,1,0|23,18,1,0|
  130reveal%=0:scan%=0:width%=1
  140dh%=0:dhline%=0:borked%=0
  150REM Variables for downloading
  160screen%=0
  170download%=0
  180X%=OPENUP(FNwelcome)
  190FOR l%=1TO3: READ v%:BPUT#X%,v%:NEXT
  200REPEAT
  210M%=BGET#X%: IF M%=-2 THEN GOTO 340
  220IF M% >= 0 THEN PROCvdu(M%): GOTO 340
  230I%=INKEY(10):IF I%=-1 THEN PROCdhscan: GOTO 340
  240IF I%=35 OR I%=13 THEN I%=95
  250IF I%=127 THEN I%=8
  260IF I%=6 THEN PROCtogglescreen: GOTO 210
  270IF I%=16 THEN PROCdumpscreen: GOTO 210
  280IF I%=18 THEN PROCreveal: GOTO 210
  290IF I%=23 THEN PROCwidth: GOTO 210
  300IF I%=24 THEN GOTO 350
  310IF I%=8 AND borked%=1 THEN VDU8,32
  320IF I%>139 AND I%<144 THEN I%-=132
  330IF I% > 0 THEN BPUT#X%,I%
  340UNTIL M%=-2
  350CLOSE#X%
  360PRINTTAB(9,24)CHR$(128+RND(7));"Press any key to exit.";
  370A=GET: PRINT: ON: OSCLI"FX229"
  380END
  390:
  400DEFPROCvdu(M%)
  410IF M% >= 32 THEN PROCactvdu: ENDPROC
  420CASE M% OF
  430WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  440WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  450WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  460WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  470WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  480WHEN 12, 13, 30: VDU M%
  490WHEN 17: ON
  500WHEN 20: OFF
  510ENDCASE
  520IF VPOS=24 THEN VDU31,POS,0
  530ENDPROC
  540:
  550DEFPROCcheckdh
  560IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  590ENDPROC
  600:
  610DEFPROCactvdu
  620IFdhline%<>VPOS THEN dh%=0
  630VDU (M% OR 128)
  650ENDPROC
  660:
  670DEFPROCcheckline
  680IF dh%=1 THEN N%=VPOS:PROCcopyline
  710ENDPROC
  720:
  730DEFPROCdhscan
  740IFscan%=0ENDPROC
  750FOR N%=0 TO 22: FOR P%=0 TO 39
  760IF ?(&FFFF7C00+(40*N%)+P%) = 141 THEN PROCcopyline: N%+=1
  770NEXT P%, N%
  780scan%=0
  790ENDPROC
  800:
  810DEFPROCcopyline
  820IF N% > 22 THEN ENDPROC
  830FOR P%=0 TO 36 STEP 4
  840!(&FFFF7C00+(40*(N%+1))+P%) = !(&FFFF7C00+(40*N%)+P%)
  850NEXT P%
  860ENDPROC
  870:
  880DEFPROCreveal
  890reveal% EOR=1
  900VDU23,18,2,reveal%|
  910ENDPROC
  920:
  930DEFPROCwidth
  940width% EOR=1
  950VDU23,18,255,12+(width%*4)|
  960ENDPROC
  970:
  980DEFPROCdumpscreen: REM For debug purposes, triggered on CTRL-P
  990F$="vt."+STR$screen%+".header"
 1000Q%=OPENOUT F$
 1010FOR P%=0 TO 39: B%=?(&FFFF7C00+P%)
 1020IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1030NEXT:CLOSE#Q%
 1040F$="vt."+STR$screen%+".body"
 1050Q%=OPENOUT F$
 1060FOR P%=40 TO 959: B%=?(&FFFF7C00+P%)
 1070IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1080NEXT:CLOSE#Q%:screen%+=1
 1090ENDPROC
 1100:
 1110DEFPROCtogglescreen
 1120OSCLI"Fullscreen"
 1130SYS"OS_Byte",42 TO ,fs%
 1140IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1150ENDPROC
 1160:
 1170DEFFNwelcome
 1180PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156)
 1190PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156)
 1200PRINT
 1210PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1220PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1230PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1240PRINT
 1250PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1260PRINT CHR$(131);"5 - NXtel"
 1270PRINT CHR$(131);"6 - TEEFAX - P999 to exit"
 1290PRINT
 1300PRINT CHR$(129);"0 - Exit"
 1310PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
 1320PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1330PRINT CHR$(134);" Press CTRL-R to toggle Reveal":
 1340PRINT CHR$(134);" Press CTRL-X to exit immediately": PRINT
 1350PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1360REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 55) OR A%=6 OR A%=24
 1370IF A% = 23 THEN PROCwidth:GOTO 1360
 1380IF A% = 24 OR A% = 48 THEN ON:OSCLI"FX229":END
 1390IF A% = 6 THEN PROCtogglescreen: GOTO 1360
 1400IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1410IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1420IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1430IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1440IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1450IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1460IF A% = 55 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6503"
 1470CLS
 1480="ip0:"+S$
 2000REM Telnet init data
 2010DATA&FF,&FD,&03
