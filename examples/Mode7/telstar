   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command.
   70:
   80VER$="Viewdata client version 20210328"
   90:
  100sdl%=0
  110IF (INKEY(-256) AND &A0) = &A0 THEN GOTO 140: REM RISC OS, let's hope the user knows what they're doing.
  120IF INKEY(-256) <> &4D THEN PRINT "This program works on Matrix Brandy for RISC OS or SDL only.":END
  130sdl%=1
  140:
  150SYS"OS_Byte",229,1: SYS "OS_Byte",4,1
  160OSCLI"Key1|_":OSCLI"KEY2"+CHR$(130)
  170ON ERROR GOTO 520
  180MODE 7:OFF: VDU23,16,1|23,18,1|
  190reveal%=0:scan%=0:width%=1:black%=0
  200dh%=0:dhline%=0:borked%=0:telnet%=1:extended%=0
  210allowfs%=1:DIM scr%% 1000
  220REM Variables for downloading
  230screen%=0
  240download%=0
  250X%=OPENUP("ip0:"+FNwelcome)
  260IF sdl% THEN PROCstatusbar(CHR$132+"] "+S$): OSCLI"WinTitle Matrix Network Viewdata/Teletext Client: Connected"
  270REM Send Telnet init data if required
  280IF telnet%=1 THEN BPUT#X%,&FF,&FD,&03 ELSE VDU23,18,1,2|:BPUT#X%,13
  290REPEAT
  300M%=BGET#X%: IF M%=-2 THEN GOTO 510
  310IF M% >= 0 THEN PROCvdu(M%):GOTO510
  320SYS "OS_Byte",21,0:I%=INKEY(20):IF I%=-1 THEN PROCdhscan: GOTO 510
  330IF I%=35 OR I%=13 THEN I%=95:GOTO500
  340IF I%>=32 AND I%<=126THENGOTO500
  350IF I%=127 THEN I%=8:GOTO410
  360IF I%=4 THEN GOTO 520
  370IF I%=6 THEN PROCtogglescreen: GOTO 300
  380IF I%=18 THEN PROCreveal: GOTO 300
  390IF I%=2 THEN PROCtoggleblack: GOTO 300
  400IF I%=24 THEN GOTO 550
  410IF I%=8 AND borked%=1 THEN VDU8,32:GOTO500
  420IF I%>=136 AND I%<=139 THEN I%-=128
  430IF I%=31THENPROChelp:GOTO300
  440IF I%=130THENI%=FNcontrolcode:GOTO500
  450IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO500
  460IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO500
  470IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO500
  480IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO500
  490IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  500IF I% > 0 THEN BPUT#X%,I%
  510UNTIL M%=-2
  520CLOSE#X%
  530IF sdl% THEN PROCstatusbar(CHR$129+"Disconnected  "+CHR$156): *WinTitle Matrix Network Viewdata/Teletext Client: Disconnected
  540VDU26:PRINTTAB(18,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  550CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  560:
  570DEFPROCvdu(M%)
  580IF M% >= 32 THEN PROCactvdu: ENDPROC
  590CASE M% OF
  600WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  610WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  620WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  630WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  640WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  650WHEN 12, 13, 30: VDU M%
  660WHEN 17: ON
  670WHEN 20: OFF
  680ENDCASE
  690IF VPOS=24 THEN VDU31,POS,0
  700ENDPROC
  710:
  720DEFPROCcheckdh
  730IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  740ENDPROC
  750:
  760DEFPROCactvdu
  770IFdhline%<>VPOS THEN dh%=0
  780VDU (M% OR 128):IF VPOS=24 OR (VPOS=23 AND POS=40) THEN VDU30
  790ENDPROC
  800:
  810DEFPROCcheckline
  820IF dh%=0 THEN ENDPROC
  830N%=VPOS:PROCcopyline
  840ct%=0:REPEAT:dm%=BGET#X%:IFdm%<>27THENct%+=1
  850UNTIL ct%=40 OR dm%=10 OR dm%=11
  860IF dm%=11 THEN VDU11 ELSE VDU10
  870ENDPROC
  880:
  890DEFPROCdhscan
  900IFscan%=0ENDPROC
  910FOR N%=0 TO 22: FOR P%=0 TO 39
  920IF GET(P%,N%) = 141 THEN PROCcopyline: N%+=1
  930NEXT P%, N%
  940scan%=0
  950ENDPROC
  960:
  970DEFPROCcopyline
  980IF N% > 22 THEN ENDPROC
  990x%=POS:y%=VPOS
 1000VDU31,0,N%+1
 1010FOR P%=0 TO 39: VDU GET(P%,N%): NEXT
 1020VDU31,x%,y%
 1030ENDPROC
 1040:
 1050DEFPROCreveal
 1060reveal% EOR=1
 1070VDU23,18,2,reveal%|
 1080ENDPROC
 1090:
 1100DEFPROCtoggleblack
 1110black% EOR=1
 1120VDU23,18,3,black%|
 1130ENDPROC
 1140:
 1150DEFPROCtogglescreen
 1160IF allowfs%=0THENENDPROC
 1170OSCLI"Fullscreen"
 1180SYS"OS_Byte",42 TO ,fs%
 1190IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1200ENDPROC
 1210:
 1220DEFPROCstatusbar(status$)
 1230x%=POS:y%=VPOS: VDU26
 1240PRINT TAB(0,24)SPC(40);
 1250PRINT TAB(0,24)CHR$157;LEFT$(status$, 37);
 1260VDU28,0,23,39,0
 1270PRINT TAB(x%,y%);
 1280ENDPROC
 1290:
 1300DEFFNwelcome
 1310PROCwelcomescreen
 1320REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 55) OR A%=6 OR A%=24 OR A%=84 OR A%=116
 1330IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1340IF A% = 6 THEN PROCtogglescreen: GOTO 1320
 1350IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1360IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1370IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1380IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1390IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1400IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1410IF A% = 55 THEN borked%=0: S$ = "alterant.leenooks.net:516"
 1420IF A% = 84 OR A% = 116 THEN telnet%=telnet% EOR 1: GOTO 1310
 1430CLS
 1440=S$
 1450:
 1460DEFPROCwelcomescreen:CLS:VDU26
 1470IF sdl% THEN *WinTitle Matrix Network Viewdata/Teletext Client
 1480PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1490PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1500PRINT
 1510PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1520PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1530PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1540PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1550PRINT CHR$(131);"5 - NXtel"
 1560PRINT CHR$(131);"6 - TEEFAX";CHR$(133);"and Matrix Teletext"
 1570PRINT CHR$(131);"7 - ANSItex BBS"
 1580
 1590PRINT
 1600PRINT CHR$(131);"T - Toggle Telnet codes -";
 1610IF telnet%=1 THEN PRINT CHR$(130);"ON" ELSE PRINT CHR$(129);"OFF"
 1620PRINT
 1630PRINT CHR$(129);"0 - Exit"
 1640PRINT TAB(0,15)CHR$(134);"While the session is running:"
 1650IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1660PRINT CHR$(134);" Press CTRL-R to toggle Reveal"
 1670PRINT CHR$(134);" Press CTRL-B to toggle Black"
 1680PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1690PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1700PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1710PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1720PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1730ENDPROC
 1740:
 1750DEFPROChelp
 1760x%=POS:y%=VPOS:VDU26:PROCstorescreen:CLS
 1770PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1780PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1790PRINT
 1800PRINT CHR$(134);"Special keys available:":PRINT
 1810IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1820PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1830PRINT CHR$(134);" Press";CHR$(131);"CTRL-B";CHR$(134);"to toggle Black";CHR$(133);"*"
 1840PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1850PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1860PRINT
 1870PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 1880PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 1890PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 1900PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 1910PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 1920PRINT
 1930PRINT CHR$(130);" F2: Send teletext control code":PRINT
 1940PRINT CHR$(133);"*";CHR$(134);"This also switches the Hold Character"
 1950PRINT CHR$(134);"behaviour between that of the";CHR$(131);"SAA5050";CHR$(134);"/"
 1960PRINT CHR$(131);"BBC Micro";CHR$(134);"and later Teletext/Videotex"
 1970PRINT CHR$(134);"specifications."
 1980IFGET
 1990PROCrestorescreen
 2000VDU28,0,23,39,0,31,x%,y%
 2010ENDPROC
 2020:
 2030DEFFNcontrolcode
 2040x%=POS:y%=VPOS:VDU26:PROCstorescreen:CLS
 2050VDU23,18,3,1|
 2060PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 2070PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 2080PRINT
 2090PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2100PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 2110PRINT " @:(text black)     P:(graphics black)"
 2120PRINT CHR$(129);"A: text red        Q: graphics red"
 2130PRINT CHR$(130);"B: text green      R: graphics green"
 2140PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 2150PRINT CHR$(132);"D: text blue       T: graphics blue"
 2160PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 2170PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 2180PRINT CHR$(135);"G: text white      W: graphics white"
 2190PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 2200PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 2210PRINT " J: &8A (End Box)   Z: Separated gfx"
 2220PRINT " K: &8B (Start Box)";CHR$&9B;"[: &9B (Esc/Switch)"
 2230PRINT " L: Dbl Height Off ";CHR$&9B;"|: Black background"
 2240PRINT " M: Dbl Height On  ";CHR$&9B;"]: Set bg colour"
 2250PRINT " N: &8E (dbl width)";CHR$&9B;"^: Hold graphics"
 2260PRINT " O: &8F (dbl size) ";CHR$&9B;"#: Release graphics"
 2270PRINT
 2280PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2290PRINT CHR$(134);"have no effect in this client."
 2300t%=GET
 2310SYS6,25,16
 2320PROCrestorescreen
 2330VDU28,0,23,39,0,31,x%,y%,23,18,3,0|
 2340=(t% AND 31)+128
 2350:
 2360DEFPROCstorescreen
 2370FOR p%=0 TO 999
 2380p%?scr%%=GET(p% MOD 40,p% DIV 40)
 2390NEXT
 2400ENDPROC
 2410:
 2420DEFPROCrestorescreen
 2430VDU30: FOR p%=0 TO 999: VDU (p%?scr%%) OR &80: NEXT
 2440ENDPROC
