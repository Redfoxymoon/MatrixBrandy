   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command. The VDU23 commands in PROCdhscan and PROCreveal
   70REM are from RISC OS 5.
   80:
   90*FX229,1
  100*WinTitle Matrix Brandy BASIC Telstar Client
  110REMON ERROR GOTO 320
  120MODE 7:OFF
  130reveal%=0:scan%=0
  140dh%=0:dhline%=0:borked%=0
  150REM Variables for downloading
  160screen%=0
  170download%=0
  180X%=OPENUP FNwelcome
  190BPUT#X%,13
  200REPEAT
  210M%=BGET#X%: IF M%=-2 THEN GOTO 320
  220IF M% >= 0 THEN PROCvdu(M%): GOTO 320
  230PROCdhscan
  240I%=INKEY(10):IF I%=35 OR I%=13 THEN I%=95
  245IF I%=127 THEN I%=8
  250IF I%=-1 THEN GOTO 320
  260IF I%=6 THEN OSCLI"FullScreen": GOTO 210
  270IF I%=16 THEN PROCdumpscreen: GOTO 210
  280IF I%=18 THEN PROCreveal: GOTO 210
  290IF I%=24 THEN GOTO 330
  295IF I%=8 AND borked%=1 THEN VDU8,32
  300IF I%>139 AND I%<144 THEN I%=I%-132
  310IF I% > 0 THEN BPUT#X%,I%
  320UNTIL M%=-2
  330CLOSE#X%:
  340PRINTTAB(9,24)CHR$(128+RND(7));"Press any key to exit.";
  350A=GET: PRINT: ON
  360END
  370:
  380DEFPROCvdu(M%)
  390IF M% >= 32 THEN PROCactvdu: ENDPROC
  400CASE M% OF
  410WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: VDU (N%+64): PROCcheckdh: ENDPROC
  420WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  430WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  440WHEN 10: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  450WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  460WHEN 12, 13, 30: VDU M%
  470WHEN 17: ON
  480WHEN 20: OFF
  490ENDCASE
  500IF VPOS=24 THEN VDU31,POS,0
  510ENDPROC
  520:
  530DEFPROCcheckdh
  540IFN%=77THEN dh%=1:dhline%=VPOS:scan%=1
  550IFN%=76THEN dh%=0
  560IFN%=77ORN%=76THENVDU8,10,N%+64,11
  570ENDPROC
  580:
  590DEFPROCactvdu
  600IFdhline%<>VPOS THEN dh%=0
  610VDU (M% OR 128)
  620IFdh%=1 THEN VDU8,10,(M% OR 128),11
  630ENDPROC
  640:
  650DEFPROCdhscan
  660IFscan%=0THEN ENDPROC
  670VDU23,18,1,0|
  680FOR N%=0 TO 24: FOR P%=0 TO 39
  690IF ?(&FFFF7C00+(40*N%)+P%) = 141 THEN PROCcopyline: N%=N%+1
  700NEXT P%, N%
  710VDU23,18,1,2|:scan%=0
  720ENDPROC
  730:
  740DEFPROCcopyline
  750FOR P%=0 TO 36 STEP 4
  760!(&FFFF7C00+(40*(N%+1))+P%) = !(&FFFF7C00+(40*N%)+P%)
  770NEXT P%
  780ENDPROC
  790:
  800DEFPROCreveal
  810reveal% = reveal% EOR 1
  820VDU23,18,2,reveal%|
  830ENDPROC
  840:
  850DEFPROCcursor_up
  860IF VPOS > 0 THEN VDU11
  870ENDPROC
  880:
  890DEFPROCdumpscreen: REM For debug purposes, triggered on CTRL-P
  900F$="vt."+STR$screen%+".header"
  910Q%=OPENOUT F$
  920FOR P%=0 TO 39: B%=?(&FFFF7C00+P%)
  930IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
  940NEXT:CLOSE#Q%
  950F$="vt."+STR$screen%+".body"
  960Q%=OPENOUT F$
  970FOR P%=40 TO 959: B%=?(&FFFF7C00+P%)
  980IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
  990NEXT:CLOSE#Q%:screen%+=1
 1000ENDPROC
 1010:
 1020DEFFNwelcome
 1030PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156);
 1040PRINT CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Brandy BASIC Telstar Client ";CHR$(156);
 1050PRINT
 1060PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1070PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1080PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1090PRINT
 1100PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1110PRINT
 1120PRINT CHR$(129);"0 - Exit"
 1130PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
 1140PRINT CHR$(134);"  Press CTRL-F to toggle Full Screen":
 1150PRINT CHR$(134);"  Press CTRL-R to toggle Reveal":
 1160PRINT CHR$(134);"  Press CTRL-X to exit immediately": PRINT
 1170PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1180REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 52) OR A%=6 OR A%=23 OR A%=24
 1190IF A% = 24 OR A% = 48 THEN ON:END
 1200IF A% = 6 THEN OSCLI"FullScreen": GOTO 1180
 1210IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1220IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1230IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1240IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1250CLS
 1260="ip0:"+S$
