   10MODE 7:OFF
   20R%=0
   30S$=FNwelcome
   40ON ERROR GOTO 170
   50X%=OPENUP S$
   60BPUT#X%,13
   70REPEAT M%=BGET#X%: UNTIL M% <> -1
   80PROCvdu(M%)
   90REPEAT
  100M%=BGET#X%
  110I%=INKEY(1):IF I%=35 THEN I%=95
  120IF I%=18 THEN PROCreveal: GOTO 110
  130IF I%=24 THEN PRINT:GOTO 170
  140IF I% > 0 THEN BPUT#X%,I%
  150IF M% >= 0 THEN PROCvdu(M%)
  160UNTIL M%=-2
  170CLOSE#X%:PROCdhscan: ON
  180END
  190DEFPROCvdu(M%)
  200IF M%<>27 THEN PROCactvdu(M%): ENDPROC
  210REPEAT M%=BGET#X%:UNTIL M%<>-1
  220VDU (M%+64): PROCdhscan
  230ENDPROC
  240DEFPROCactvdu(M%)
  250IF M% = &11 THEN ON: ENDPROC
  260IF M% = &14 THEN OFF: ENDPROC
  270IF M% > 32 THEN VDU (M% OR 128) ELSE VDU M%
  280ENDPROC
  290DEFPROCdhscan
  300FOR N%=0 TO 24
  310FOR P%=0 TO 39
  320IF ?(&FFFF7C00+(40*N%)+P%) = 141 THEN PROCcopyline: N%=N%+1
  330NEXT P%, N%
  340ENDPROC
  350DEFPROCcopyline
  360FOR P%=0 TO 39
  370?(&FFFF7C00+(40*(N%+1))+P%) = ?(&FFFF7C00+(40*N%)+P%)
  380NEXT P%
  390ENDPROC
  400DEFPROCreveal
  410IF R%=0 THEN R%=1 ELSE R%=0
  420VDU23,18,2,R%|
  430ENDPROC
  440DEFFNwelcome
  450PRINT CHR$(130);CHR$(141);" Matrix Brandy BASIC Telstar Client"
  460PRINT CHR$(130);CHR$(141);" Matrix Brandy BASIC Telstar Client"
  470PRINT
  480PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
  490PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
  500PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
  510PRINT
  520PRINT CHR$(133);"4 - Tetrachloromethane"
  530PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
  530PRINT CHR$(134);"   Press CTRL-R to toggle Reveal": PRINT
  540PRINT CHR$(134);"   Press CTRL-X to exit": PRINT
  550PRINT TAB(4,23)CHR$(129)"Please make your selection..."
  560REPEAT A%=GET: UNTIL A% >= 49 AND A% <= 52
  570IF A% = 49 THEN S$ = "ip4:glasstty.com:6502"
  580IF A% = 50 THEN S$ = "ip4:glasstty.com:6503"
  590IF A% = 51 THEN S$ = "ip4:glasstty.com:6504"
  600IF A% = 52 THEN S$ = "ip4:fish.ccl4.org:23"
  610CLS
  620=S$
