   10MODE 7:OFF
   20PROCHINIT
   30R%=0
   40ON ERROR GOTO 170
   50X%=OPENUP"ip4:glasstty.com:6502"
   60BPUT#X%,13
   70REPEAT M%=BGET#X%: UNTIL M% <> -1
   80PROCVDU(M%)
   90REPEAT
  100M%=BGET#X%
  110I%=INKEY(1):IF I%=35 THEN I%=95
  120IF I%=18 THEN PROCREVEAL: GOTO 110
  130IF I%=24 THEN PRINT:GOTO 170
  140IF I% > 0 THEN BPUT#X%,I%
  150IF M% >= 0 THEN PROCVDU(M%)
  160UNTIL M%=-2
  170CLOSE#X%:PROCDHSCAN: ON
  180END
  190DEFPROCVDU(M%)
  200IF M%<>27 THEN PROCACTVDU(M%): ENDPROC
  210REPEAT M%=BGET#X%:UNTIL M%<>-1
  220VDU (M%+64): PROCDHSCAN
  230ENDPROC
  240DEFPROCACTVDU(M%)
  250IF M% = &11 THEN ON: ENDPROC
  260IF M% = &14 THEN OFF: ENDPROC
  270IF M% > 32 THEN VDU (M% OR 128) ELSE VDU M%
  280ENDPROC
  290DEFPROCDHSCAN
  300FOR N%=0 TO 24
  310FOR P%=0 TO 39
  320IF ?(&FFFF7C00+(40*N%)+P%) = 141 THEN PROCCOPYLINE: N%=N%+1
  330NEXT P%, N%
  340ENDPROC
  350DEFPROCCOPYLINE
  360FOR P%=0 TO 39
  370?(&FFFF7C00+(40*(N%+1))+P%) = ?(&FFFF7C00+(40*N%)+P%)
  380NEXT P%
  390ENDPROC
  400DEFPROCREVEAL
  410IF R%=0 THEN R%=1 ELSE R%=0
  420VDU23,18,2,R%|
  430ENDPROC
  440DEFPROCHINIT
  450PRINT CHR$(130);CHR$(141);" Matrix Brandy BASIC Telstar Client"
  460PRINT CHR$(130);CHR$(141);" Matrix Brandy BASIC Telstar Client"
  470PRINT
  480PRINT CHR$(134);"   Press CTRL-R to toggle Reveal": PRINT
  490PRINT CHR$(134);"   Press CTRL-X to exit": PRINT
  500PRINT TAB(4,23)CHR$(129)"Press any key to continue..."
  510DUMMY=GET
  520CLS
  530ENDPROC
