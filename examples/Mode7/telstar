   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command.
   70:
   80VER$="Viewdata client version 20200623"
   90:
  100sdl%=0
  110IF (INKEY(-256) AND &A0) = &A0 THEN GOTO 140: REM RISC OS, let's hope the user knows what they're doing.
  120IF INKEY(-256) <> &4D THEN PRINT "This program works on Matrix Brandy for RISC OS or SDL only.":END
  130sdl%=1
  140:
  150SYS"OS_Byte",229,1: SYS "OS_Byte",4,1: IF sdl% THEN SYS "Brandy_RefreshInterval",0
  160OSCLI"Key1|_":OSCLI"KEY2"+CHR$(130)
  170IF sdl% THEN *WinTitle Matrix Network Viewdata/Teletext Client
  180ON ERROR GOTO 510
  190MODE 7:OFF: VDU23,16,1,0|23,18,1,0|
  200reveal%=0:scan%=0:width%=1
  210dh%=0:dhline%=0:borked%=0:telnet%=1:extended%=0
  220allowfs%=1:DIM scr%% 1000
  230REM Variables for downloading
  240screen%=0
  250download%=0
  260X%=OPENUP(FNwelcome)
  270REM Send Telnet init data if required
  280IF telnet%=1 THEN BPUT#X%,&FF,&FD,&03 ELSE VDU23,18,1,2|:BPUT#X%,13
  290REPEAT
  300M%=BGET#X%: IF M%=-2 THEN GOTO 500
  310IF M% >= 0 THEN PROCvdu(M%):GOTO500
  320SYS "OS_Byte",21,0:I%=INKEY(20):IF I%=-1 THEN PROCdhscan: GOTO 500
  330IF I%=35 OR I%=13 THEN I%=95:GOTO490
  340IF I%>=32 AND I%<=126THENGOTO490
  350IF I%=127 THEN I%=8:GOTO400
  360IF I%=4 THEN GOTO 510
  370IF I%=6 THEN PROCtogglescreen: GOTO 300
  380IF I%=18 THEN PROCreveal: GOTO 300
  390IF I%=24 THEN GOTO 530
  400IF I%=8 AND borked%=1 THEN VDU8,32:GOTO490
  410IF I%>=136 AND I%<=139 THEN I%-=128
  420IF I%=31THENPROChelp:GOTO300
  430IF I%=130THENI%=FNcontrolcode:GOTO490
  440IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO490
  450IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO490
  460IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO490
  470IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO490
  480IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  490IF I% > 0 THEN BPUT#X%,I%
  500UNTIL M%=-2
  510CLOSE#X%
  520PRINTTAB(12,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  530CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  540:
  550DEFPROCvdu(M%)
  560IF M% >= 32 THEN PROCactvdu: ENDPROC
  570CASE M% OF
  580WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  590WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  600WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  610WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  620WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  630WHEN 12, 13, 30: VDU M%
  640WHEN 17: ON
  650WHEN 20: OFF
  660ENDCASE
  670IF VPOS=24 THEN VDU31,POS,0
  680ENDPROC
  690:
  700DEFPROCcheckdh
  710IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  720ENDPROC
  730:
  740DEFPROCactvdu
  750IFdhline%<>VPOS THEN dh%=0
  760VDU (M% OR 128):IF VPOS=24 OR (VPOS=23 AND POS=40) THEN VDU30
  770ENDPROC
  780:
  790DEFPROCcheckline
  800IF dh%=0 THEN ENDPROC
  810N%=VPOS:PROCcopyline
  820ct%=0:REPEAT:dm%=BGET#X%:IFdm%<>27THENct%+=1
  830UNTIL ct%=40 OR dm%=10 OR dm%=11
  840IF dm%=11 THEN VDU11 ELSE VDU10
  850ENDPROC
  860:
  870DEFPROCdhscan
  880IFscan%=0ENDPROC
  890FOR N%=0 TO 22: FOR P%=0 TO 39
  900IF GET(P%,N%) = 141 THEN PROCcopyline: N%+=1
  910NEXT P%, N%
  920scan%=0
  930ENDPROC
  940:
  950DEFPROCcopyline
  960IF N% > 22 THEN ENDPROC
  970x%=POS:y%=VPOS
  980VDU31,0,N%+1
  990FOR P%=0 TO 39: VDU GET(P%,N%): NEXT
 1000VDU31,x%,y%
 1010ENDPROC
 1020:
 1030DEFPROCreveal
 1040reveal% EOR=1
 1050VDU23,18,2,reveal%|
 1060ENDPROC
 1070:
 1080DEFPROCtogglescreen
 1090IF allowfs%=0THENENDPROC
 1100OSCLI"Fullscreen"
 1110SYS"OS_Byte",42 TO ,fs%
 1120IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1130ENDPROC
 1140:
 1150DEFFNwelcome
 1160PROCwelcomescreen
 1170REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 54) OR A%=6 OR A%=24 OR A%=84 OR A%=116
 1180IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1190IF A% = 6 THEN PROCtogglescreen: GOTO 1170
 1200IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1210IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1220IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1230IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1240IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1250IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1260IF A% = 84 OR A% = 116 THEN telnet%=telnet% EOR 1: GOTO 1160
 1270CLS
 1280="ip0:"+S$
 1290:
 1300DEFPROCwelcomescreen:CLS
 1310PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1320PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1330PRINT
 1340PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1350PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1360PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1370PRINT
 1380PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1390PRINT CHR$(131);"5 - NXtel"
 1400PRINT CHR$(131);"6 - TEEFAX";CHR$(133);"and Matrix Teletext"
 1410PRINT
 1420PRINT CHR$(131);"T - Toggle Telnet codes -";
 1430IF telnet%=1 THEN PRINT CHR$(130);"ON" ELSE PRINT CHR$(129);"OFF"
 1440PRINT
 1450PRINT CHR$(129);"0 - Exit"
 1460PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
 1470IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1480PRINT CHR$(134);" Press CTRL-R to toggle Reveal":
 1490PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1500PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1510PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1520PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1530PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1540ENDPROC
 1550:
 1560DEFPROChelp
 1570x%=POS:y%=VPOS:PROCstorescreen:CLS
 1580PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1590PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1600PRINT
 1610PRINT CHR$(134);"Special keys available:":PRINT
 1620IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1630PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1640PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1650PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1660PRINT
 1670PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 1680PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 1690PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 1700PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 1710PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 1720PRINT
 1730PRINT CHR$(130);" F2: Send teletext control code"
 1740IFGET
 1750PROCrestorescreen
 1760VDU31,x%,y%
 1770ENDPROC
 1780:
 1790DEFFNcontrolcode
 1800x%=POS:y%=VPOS:PROCstorescreen:CLS
 1810VDU23,18,3,1|
 1820PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1830PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1840PRINT
 1850PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 1860PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 1870PRINT " @:(text black)     P:(graphics black)"
 1880PRINT CHR$(129);"A: text red        Q: graphics red"
 1890PRINT CHR$(130);"B: text green      R: graphics green"
 1900PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 1910PRINT CHR$(132);"D: text blue       T: graphics blue"
 1920PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 1930PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 1940PRINT CHR$(135);"G: text white      W: graphics white"
 1950PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 1960PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 1970PRINT " J: &8A (End Box)   Z: Separated gfx"
 1980PRINT " K: &8B (Start Box)";CHR$&9B;"[: &9B (Esc/Switch)"
 1990PRINT " L: Dbl Height Off ";CHR$&9B;"|: Black background"
 2000PRINT " M: Dbl Height On  ";CHR$&9B;"]: Set bg colour"
 2010PRINT " N: &8E (dbl width)";CHR$&9B;"^: Hold graphics"
 2020PRINT " O: &8F (dbl size) ";CHR$&9B;"#: Release graphics"
 2030PRINT
 2040PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2050PRINT CHR$(134);"have no effect in this client."
 2060t%=GET
 2070SYS6,25,16
 2080PROCrestorescreen
 2090VDU31,x%,y%,23,18,3,0|
 2100=(t% AND 31)+128
 2110:
 2120DEFPROCstorescreen
 2130FOR p%=0 TO 999
 2140p%?scr%%=GET(p% MOD 40,p% DIV 40)
 2150NEXT
 2160ENDPROC
 2170:
 2180DEFPROCrestorescreen
 2190VDU30: FOR p%=0 TO 999: VDU (p%?scr%%) OR &80: NEXT
 2200ENDPROC
