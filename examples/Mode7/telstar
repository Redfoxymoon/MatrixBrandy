   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command.
   70:
   80VER$="Viewdata client version 20200522"
   90:
   95IF INKEY(-256) <> &4D THEN PRINT"This program only runs on Matrix Brandy":END
  100SYS "Brandy_GetVideoDriver" TO v$,,,,v%
  110IFv$="no_sdl" THEN PRINT"This program requires a graphics display":END
  120:
  130SYS"OS_Byte",229,1: SYS "OS_Byte",4,1
  140OSCLI"Key1"+CHR$(31)
  150*WinTitle Matrix Network Viewdata/Teletext Client
  160ON ERROR GOTO 510
  170MODE 7:OFF: VDU23,16,1,0|23,18,1,0|
  180IFv$="fbcon"THENMOUSEOFF
  190reveal%=0:scan%=0:width%=1
  200dh%=0:dhline%=0:borked%=0:telnet%=1:extended%=0
  210allowfs%=1:DIM scr% 1000
  220REM Variables for downloading
  230screen%=0
  240download%=0
  250X%=OPENUP(FNwelcome)
  260REM Send Telnet init data if required
  270IF telnet%=1 THEN BPUT#X%,&FF,&FD,&03 ELSE VDU23,18,1,2|:BPUT#X%,13
  280REPEAT
  290M%=BGET#X%: IF M%=-2 THEN GOTO 500
  300IF M% >= 0 THEN PROCvdu(M%): GOTO 500
  310I%=INKEY(10):IF I%=-1 THEN PROCdhscan: GOTO 500
  320IF I%=35 OR I%=13 THEN I%=95:GOTO490
  330IF I%>=32 AND I%<=126THENGOTO490
  340IF I%=127 THEN I%=8:GOTO400
  350IF I%=4 THEN GOTO 510
  360IF I%=6 THEN PROCtogglescreen: GOTO 290
  370IF I%=16 THEN PROCdumpscreen: GOTO 290
  380IF I%=18 THEN PROCreveal: GOTO 290
  390IF I%=24 THEN GOTO 530
  400IF I%=8 AND borked%=1 THEN VDU8,32:GOTO490
  410IF I%>=136 AND I%<=139 THEN I%-=128
  420IF I%=31THENPROChelp:GOTO290
  430IF I%=130THENI%=FNcontrolcode:GOTO490
  440IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO490
  450IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO490
  460IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO490
  470IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO490
  480IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  490IF I% > 0 THEN BPUT#X%,I%
  500UNTIL M%=-2
  510CLOSE#X%
  520PRINTTAB(12,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  530CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  540:
  550DEFPROCvdu(M%)
  560IF M% >= 32 THEN PROCactvdu: ENDPROC
  570CASE M% OF
  580WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  590WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  600WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  610WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  620WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  630WHEN 12, 13, 30: VDU M%
  640WHEN 17: ON
  650WHEN 20: OFF
  660ENDCASE
  670IF VPOS=24 THEN VDU31,POS,0
  680ENDPROC
  690:
  700DEFPROCcheckdh
  710IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  720ENDPROC
  730:
  740DEFPROCactvdu
  750IFdhline%<>VPOS THEN dh%=0
  760VDU (M% OR 128):IF VPOS=24 OR (VPOS=23 AND POS=40) THEN VDU30
  770ENDPROC
  780:
  790DEFPROCcheckline
  800IF dh%=0 THEN ENDPROC
  810N%=VPOS:PROCcopyline
  820ct%=0:REPEAT:dm%=BGET#X%:IFdm%<>27THENct%+=1
  830UNTIL ct%=40 OR dm%=10 OR dm%=11
  840IF dm%=11 THEN VDU11 ELSE VDU10
  850ENDPROC
  860:
  870DEFPROCdhscan
  880IFscan%=0ENDPROC
  890FOR N%=0 TO 22: FOR P%=0 TO 39
  900IF ?(v%+(40*N%)+P%) = 141 THEN PROCcopyline: N%+=1
  910NEXT P%, N%
  920scan%=0
  930ENDPROC
  940:
  950DEFPROCcopyline
  960IF N% > 22 THEN ENDPROC
  970FOR P%=0 TO 36 STEP 4
  980!(v%+(40*(N%+1))+P%) = !(v%+(40*N%)+P%)
  990NEXT P%
 1000ENDPROC
 1010:
 1020DEFPROCreveal
 1030reveal% EOR=1
 1040VDU23,18,2,reveal%|
 1050ENDPROC
 1060:
 1070DEFPROCdumpscreen: REM For debug purposes, triggered on CTRL-P
 1080F$="vt."+STR$screen%+".header"
 1090Q%=OPENOUT F$
 1100FOR P%=0 TO 39: B%=?(v%+P%)
 1110IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1120NEXT:CLOSE#Q%
 1130F$="vt."+STR$screen%+".body"
 1140Q%=OPENOUT F$
 1150FOR P%=40 TO 959: B%=?(v%+P%)
 1160IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1170NEXT:CLOSE#Q%:screen%+=1
 1180ENDPROC
 1190:
 1200DEFPROCtogglescreen
 1210IF allowfs%=0THENENDPROC
 1220OSCLI"Fullscreen"
 1230SYS"OS_Byte",42 TO ,fs%
 1240IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1250ENDPROC
 1260:
 1270DEFFNwelcome
 1280PROCwelcomescreen
 1290REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 54) OR A%=6 OR A%=24 OR A%=84 OR A%=116
 1300IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1310IF A% = 6 THEN PROCtogglescreen: GOTO 1290
 1320IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1330IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1340IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1350IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1360IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1370IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1380IF A% = 84 OR A% = 116 THEN telnet%=telnet% EOR 1: GOTO 1280
 1390CLS
 1400="ip0:"+S$
 1410:
 1420DEFPROCwelcomescreen:CLS
 1430PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1440PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1450PRINT
 1460PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1470PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1480PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1490PRINT
 1500PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1510PRINT CHR$(131);"5 - NXtel"
 1520PRINT CHR$(131);"6 - TEEFAX";CHR$(133);"and Matrix Teletext"
 1530PRINT
 1540PRINT CHR$(131);"T - Toggle Telnet codes -";
 1550IF telnet%=1 THEN PRINT CHR$(130);"ON" ELSE PRINT CHR$(129);"OFF"
 1560PRINT
 1570PRINT CHR$(129);"0 - Exit"
 1580PRINT TAB(0,15)CHR$(134);"While the session is running:":PRINT
 1590IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1600PRINT CHR$(134);" Press CTRL-R to toggle Reveal":
 1610PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1620PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1630PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1640PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1650PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1660ENDPROC
 1670:
 1680DEFPROChelp
 1690x%=POS:y%=VPOS:FOR p%=0 TO 999: p%?scr%=p%?v%: NEXT:CLS
 1700PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1710PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1720PRINT
 1730PRINT CHR$(134);"Special keys available:":PRINT
 1740IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1750PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1760PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1770PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1780PRINT
 1790PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 1800PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 1810PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 1820PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 1830PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 1840PRINT
 1850PRINT CHR$(130);" F2: Send teletext control code"
 1860IFGET
 1870VDU31,x%,y%
 1880FOR p%=0 TO 999: p%?v%=p%?scr%: NEXT
 1890ENDPROC
 1900:
 1910DEFFNcontrolcode
 1920x%=POS:y%=VPOS:FOR p%=0 TO 999: p%?scr%=p%?v%: NEXT:CLS
 1930VDU23,18,3,1|
 1940PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1950PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1960PRINT
 1970PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 1980PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 1990PRINT " @:(text black)     P:(graphics black)"
 2000PRINT CHR$(129);"A: text red        Q: graphics red"
 2010PRINT CHR$(130);"B: text green      R: graphics green"
 2020PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 2030PRINT CHR$(132);"D: text blue       T: graphics blue"
 2040PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 2050PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 2060PRINT CHR$(135);"G: text white      W: graphics white"
 2070PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 2080PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 2090PRINT " J: &8A (End Box)   Z: Separated gfx"
 2100PRINT " K: &8B (Start Box)";CHR$&9B;"[: &9B (Esc/Switch)"
 2110PRINT " L: Dbl Height Off ";CHR$&9B;"|: Black background"
 2120PRINT " M: Dbl Height On  ";CHR$&9B;"]: Set bg colour"
 2130PRINT " N: &8E (dbl width)";CHR$&9B;"^: Hold graphics"
 2140PRINT " O: &8F (dbl size) ";CHR$&9B;"#: Release graphics"
 2150PRINT
 2160PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2170PRINT CHR$(134);"have no effect in this client."
 2180t%=GET
 2190SYS6,25,16
 2200VDU31,x%,y%,23,18,3,0|
 2210FOR p%=0 TO 999: p%?v%=p%?scr%: NEXT
 2220=(t% AND 31)+128
