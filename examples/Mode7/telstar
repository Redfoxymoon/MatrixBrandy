   10REM > telstar
   20:
   30REM This is a basic Viewdata/Videotex client written in BBC BASIC. but
   40REM using some Matrix Brandy extensions. For instance, the networking
   50REM interface is a local extension, as is the screen memory access and
   60REM the *WinTitle command. The VDU23 commands in PROCdhscan and PROCreveal
   70REM are from RISC OS 5.
   80:
   90VER$="Viewdata client version 20191204"
  100:
  110SYS "Brandy_GetVideoDriver" TO v$,,,,v%
  120IFv$="no_sdl" THEN PRINT"This program requires a graphics display":END
  130:
  140*FX229,1
  150OSCLI"Key1"+CHR$(31)
  160*WinTitle Matrix Network Viewdata/Teletext Client
  170ON ERROR GOTO 520
  180MODE 7:OFF: VDU23,16,1,0|23,18,1,0|
  190IFv$="fbcon"THENMOUSEOFF
  200reveal%=0:scan%=0:width%=1
  210dh%=0:dhline%=0:borked%=0
  220allowfs%=1:DIM scr% 1000
  230REM Variables for downloading
  240screen%=0
  250download%=0
  260X%=OPENUP(FNwelcome)
  270BPUT#X%,&FF,&FD,&03:REM Telnet init data
  280REPEAT
  290M%=BGET#X%: IF M%=-2 THEN GOTO 510
  300IF M% >= 0 THEN PROCvdu(M%): GOTO 510
  310I%=INKEY(10):IF I%=-1 THEN PROCdhscan: GOTO 510
  320IF I%=35 OR I%=13 THEN I%=95:GOTO500
  330IF I%>=32 AND I%<=126THENGOTO500
  340IF I%=127 THEN I%=8:GOTO410
  350IF I%=4 THEN GOTO 520
  360IF I%=6 THEN PROCtogglescreen: GOTO 290
  370IF I%=16 THEN PROCdumpscreen: GOTO 290
  380IF I%=18 THEN PROCreveal: GOTO 290
  390IF I%=23 THEN PROCwidth: GOTO 290
  400IF I%=24 THEN GOTO 540
  410IF I%=8 AND borked%=1 THEN VDU8,32:GOTO500
  420IF I%>=204 AND I%<=207 THEN I%-=196
  430IF I%=31THENPROChelp:GOTO290
  440IF I%=130THENI%=FNcontrolcode:GOTO500
  450IF I%=159ORI%=203THENBPUT#X%,42:BPUT#X%,48:I%=48:GOTO500
  460IF I%=158ORI%=202THENBPUT#X%,42:BPUT#X%,48:I%=57:GOTO500
  470IF I%=172ORI%=236THENBPUT#X%,42:I%=95:GOTO500
  480IF I%=175ORI%=239THENBPUT#X%,42:BPUT#X%,48:I%=95:GOTO500
  490IF I%=171ORI%=233THENBPUT#X%,42:BPUT#X%,57:BPUT#X%,48:I%=95
  500IF I% > 0 THEN BPUT#X%,I%
  510UNTIL M%=-2
  520CLOSE#X%
  530PRINTTAB(12,24)CHR$(128+RND(7));"Press any key...";: A=GET:RUN
  540CLOSE#X%:PRINT TAB(0,24);: ON: OSCLI"FX229":END
  550:
  560DEFPROCvdu(M%)
  570IF M% >= 32 THEN PROCactvdu: ENDPROC
  580CASE M% OF
  590WHEN 27: REPEAT N%=BGET#X%: UNTIL N%<>-1: N%=(N% MOD 32)+128: VDU N%: PROCcheckdh: ENDPROC
  600WHEN 8: IF POS=0 AND VPOS=0 THEN VDU31,39,23 ELSE VDU 8
  610WHEN 9: IF POS=39 AND VPOS=23 THEN VDU30 ELSE VDU9
  620WHEN 10: PROCcheckline: dh%=0: IF VPOS<23 THEN VDU10 ELSE VDU 31,POS,0
  630WHEN 11: IF VPOS > 0 THEN VDU11 ELSE VDU31,POS,23
  640WHEN 12, 13, 30: VDU M%
  650WHEN 17: ON
  660WHEN 20: OFF
  670ENDCASE
  680IF VPOS=24 THEN VDU31,POS,0
  690ENDPROC
  700:
  710DEFPROCcheckdh
  720IFN%=141THEN dh%=1:dhline%=VPOS:scan%=1
  730ENDPROC
  740:
  750DEFPROCactvdu
  760IFdhline%<>VPOS THEN dh%=0
  770VDU (M% OR 128)
  780ENDPROC
  790:
  800DEFPROCcheckline
  810IF dh%=0 THEN ENDPROC
  820N%=VPOS:PROCcopyline
  830ct%=0:REPEAT:dm%=BGET#X%:IFdm%<>27THENct%+=1
  840UNTIL ct%=40 OR dm%=10 OR dm%=11
  850IF dm%=11 THEN VDU11 ELSE VDU10
  860ENDPROC
  870:
  880DEFPROCdhscan
  890IFscan%=0ENDPROC
  900FOR N%=0 TO 22: FOR P%=0 TO 39
  910IF ?(v%+(40*N%)+P%) = 141 THEN PROCcopyline: N%+=1
  920NEXT P%, N%
  930scan%=0
  940ENDPROC
  950:
  960DEFPROCcopyline
  970IF N% > 22 THEN ENDPROC
  980FOR P%=0 TO 36 STEP 4
  990!(v%+(40*(N%+1))+P%) = !(v%+(40*N%)+P%)
 1000NEXT P%
 1010ENDPROC
 1020:
 1030DEFPROCreveal
 1040reveal% EOR=1
 1050VDU23,18,2,reveal%|
 1060ENDPROC
 1070:
 1080DEFPROCwidth
 1090width% EOR=1
 1100VDU23,18,255,12+(width%*4)|
 1110ENDPROC
 1120:
 1130DEFPROCdumpscreen: REM For debug purposes, triggered on CTRL-P
 1140F$="vt."+STR$screen%+".header"
 1150Q%=OPENOUT F$
 1160FOR P%=0 TO 39: B%=?(v%+P%)
 1170IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1180NEXT:CLOSE#Q%
 1190F$="vt."+STR$screen%+".body"
 1200Q%=OPENOUT F$
 1210FOR P%=40 TO 959: B%=?(v%+P%)
 1220IF B% >= &A0 THEN BPUT#Q%,(B% AND &7F) ELSE BPUT#Q%, B%
 1230NEXT:CLOSE#Q%:screen%+=1
 1240ENDPROC
 1250:
 1260DEFPROCtogglescreen
 1270IF allowfs%=0THENENDPROC
 1280OSCLI"Fullscreen"
 1290SYS"OS_Byte",42 TO ,fs%
 1300IF fs% AND 8 THEN MOUSE OFF ELSE MOUSE ON
 1310ENDPROC
 1320:
 1330DEFFNwelcome
 1340PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1350PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1360PRINT
 1370PRINT CHR$(131);"1 - Telstar";CHR$(133);"(CURRER)"
 1380PRINT CHR$(131);"2 - Telstar";CHR$(133);"(ELLIS)"
 1390PRINT CHR$(131);"3 - Telstar";CHR$(133);"(ACTON)"
 1400PRINT
 1410PRINT CHR$(131);"4 - Tetrachloromethane";CHR$(133);"(CCl4)"
 1420PRINT CHR$(131);"5 - NXtel"
 1430PRINT CHR$(131);"6 - TEEFAX - P999 to exit"
 1440PRINT
 1450PRINT CHR$(129);"0 - Exit"
 1460PRINT TAB(0,14)CHR$(134);"While the session is running:":PRINT
 1470IF allowfs%=1 THEN PRINT CHR$(134);" Press CTRL-F to toggle Full Screen":
 1480PRINT CHR$(134);" Press CTRL-R to toggle Reveal":
 1490PRINT CHR$(134);" Press CTRL-X to exit immediately"
 1500PRINT CHR$(134);" Press CTRL-D to disconnect":PRINT
 1510PRINT CHR$(130);" F1 or CTRL-/ for help & other keys"
 1520PRINT TAB(4,23)CHR$(129);"Please make your selection..."
 1530PRINT CHR$(132);CHR$(157);CHR$(135);VER$;
 1540REPEAT A%=GET: UNTIL (A% >= 48 AND A% <= 54) OR A%=6 OR A%=24
 1550IF A% = 23 THEN PROCwidth:GOTO 1540
 1560IF A% = 24 OR A% = 48 THEN PRINT:ON:OSCLI"FX229":END
 1570IF A% = 6 THEN PROCtogglescreen: GOTO 1540
 1580IF A% = 49 THEN borked%=1: S$ = "glasstty.com:6502"
 1590IF A% = 50 THEN borked%=1: S$ = "glasstty.com:6503"
 1600IF A% = 51 THEN borked%=1: S$ = "glasstty.com:6504"
 1610IF A% = 52 THEN borked%=0: S$ = "fish.ccl4.org:23"
 1620IF A% = 53 THEN borked%=0: S$ = "nx.nxtel.org:23280"
 1630IF A% = 54 THEN borked%=0: S$ = "pegasus.matrixnetwork.co.uk:6502"
 1640CLS
 1650="ip0:"+S$
 1660:
 1670DEFPROChelp
 1680x%=POS:y%=VPOS:FOR p%=0 TO 999: p%?scr%=p%?v%: NEXT:CLS
 1690PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1700PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1710PRINT
 1720PRINT CHR$(134);"Special keys available:":PRINT
 1730IF allowfs%=1 THEN PRINT CHR$(134);" Press";CHR$(131);"CTRL-F";CHR$(134);"to toggle Full Screen":
 1740PRINT CHR$(134);" Press";CHR$(131);"CTRL-R";CHR$(134);"to toggle Reveal":
 1750PRINT CHR$(134);" Press";CHR$(131);"CTRL-X";CHR$(134);"to exit immediately"
 1760PRINT CHR$(134);" Press";CHR$(131);"CTRL-D";CHR$(134);"to disconnect": PRINT
 1770PRINT
 1780PRINT CHR$(131);" CTRL-[";CHR$(134);"Select previous frame";CHR$(129);"(*#)"
 1790PRINT CHR$(131);" CTRL-^";CHR$(134);"Select main index frame";CHR$(129);"(*0#)"
 1800PRINT CHR$(131);" PgUp";CHR$(134);" redraw current frame";CHR$(129);"(*00)"
 1810PRINT CHR$(131);" PgDn";CHR$(134);" Request updated frame";CHR$(129);"(*09)"
 1820PRINT CHR$(131);" CTRL-END";CHR$(134);"Request disconnect";CHR$(129);"(*90#)"
 1830PRINT
 1840PRINT CHR$(130);" F2: Send teletext control code"
 1850IFGET
 1860VDU31,x%,y%
 1870FOR p%=0 TO 999: p%?v%=p%?scr%: NEXT
 1880ENDPROC
 1890:
 1900DEFFNcontrolcode
 1910x%=POS:y%=VPOS:FOR p%=0 TO 999: p%?scr%=p%?v%: NEXT:CLS
 1920PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1930PRINT "  ";CHR$(132);CHR$(157);CHR$(135);CHR$(141);"Matrix Network Viewdata Client ";CHR$(156)
 1940PRINT
 1950PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 1960PRINT CHR$(141);CHR$(134);SPC(10);"Control codes:"
 1970PRINT " @:(text black)     P:(graphics black)"
 1980PRINT CHR$(129);"A: text red        Q: graphics red"
 1990PRINT CHR$(130);"B: text green      R: graphics green"
 2000PRINT CHR$(131);"C: text yellow     S: graphics yellow"
 2010PRINT CHR$(132);"D: text blue       T: graphics blue"
 2020PRINT CHR$(133);"E: text magenta    U: graphics magenta"
 2030PRINT CHR$(134);"F: text cyan       V: graphics cyan"
 2040PRINT CHR$(135);"G: text white      W: graphics white"
 2050PRINT CHR$(136);"H: Flash On";CHR$(137);"       X: Conceal"
 2060PRINT CHR$(137);"I: Flash Off       Y: Contiguous gfx"
 2070PRINT " J: &8A (End Box)   Z: Separated gfx"
 2080PRINT " K: &8B (Start Box) [: &9B (Esc/Switch)"
 2090PRINT " L: Dbl Height Off  |: Black background"
 2100PRINT " M: Dbl Height On   ]: Set bg colour"
 2110PRINT " N: &8E (dbl width) ^: Hold graphics"
 2120PRINT " O: &8F (dbl size)  _: Release graphics"
 2130PRINT
 2140PRINT CHR$(134);"Note: Codes &8A, &8B, &8E, &8F and &9B"
 2150PRINT CHR$(134);"have no effect in this client."
 2160t%=GET
 2170VDU31,x%,y%
 2180FOR p%=0 TO 999: p%?v%=p%?scr%: NEXT
 2190=(t% AND 31)+128
