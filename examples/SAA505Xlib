   10REM SAA505Xlib
   20REM A library for switching the MODE 7 language settings as per the
   30REM SAA505X family of Teletext character generators from Mullard.
   40REM Currently the supported codes are: 0 / 5050: English (default),
   50REM 1 / 5051: German, 2 / 5052: Swedish, 3 / 5053: Italian,
   60REM 4 / 5054: Belgian, 5 / 5055: US ASCII, 6 / 5056: Hebrew and
   70REM 7 / 5057: Cyrillic
   80DEFPROCsaa505xlibset(code%)
   90LOCAL loop%
  100SYS "OS_Byte",42,1
  110SYS "OS_Byte",25,16: REM Reset to default
  120CASE code% OF
  130WHEN 0,5050: REM SAA5050 is the default
  140WHEN 1,5051:PROCsaa505xr(&23,&A3,1):PROCsaa505xr(&A7,&C0,1):PROCsaa505xr(&C4,&DB,1):PROCsaa505xr(&D6,&DC,1):PROCsaa505xr(&DC,&DD,1):PROCsaa505xr(&5E,&DE,1):PROCsaa505xr(&5F,&DF,1):PROCsaa505xr(&B0,&E0,1):PROCsaa505xr(&E4,&FB,1):PROCsaa505xr(&F6,&FC,1):PROCsaa505xr(&FC,&FD,1):PROCsaa505xr(&DF,&FE,1): REM German
  150WHEN 2,5052:PROCsaa505xr(&23,&A3,1):PROCsaa505xr(&A4,&A4,1):PROCsaa505xr(&C9,&C0,1):PROCsaa505xr(&C4,&DB,1):PROCsaa505xr(&D6,&DC,1):PROCsaa505xr(&C5,&DD,1):PROCsaa505xr(&DC,&DE,1):PROCsaa505xr(&5F,&DF,1):PROCsaa505xr(&E9,&E0,1):PROCsaa505xr(&E4,&FB,1):PROCsaa505xr(&F6,&FC,1):PROCsaa505xr(&E5,&FD,1):PROCsaa505xr(&FC,&FE,1): REM Swedish
  160WHEN 3,5053:PROCsaa505xr(&E9,&C0,1):PROCsaa505xr(&B0,&DB,1):PROCsaa505xr(&E7,&DC,1):PROCsaa505xr(&D9,&E0,1):PROCsaa505xr(&E0,&FB,1):PROCsaa505xr(&D2,&FC,1):PROCsaa505xr(&E8,&FD,1):PROCsaa505xr(&EC,&FE,1): REM Italian
  170WHEN 4,5054:PROCsaa505xr(&E9,&A3,1):PROCsaa505xr(&EF,&A4,1):PROCsaa505xr(&E0,&C0,1):PROCsaa505xr(&EB,&DB,1):PROCsaa505xr(&EA,&DC,1):PROCsaa505xr(&D9,&DD,1):PROCsaa505xr(&EE,&DE,1):PROCsaa505xr(&E8,&E0,1):PROCsaa505xr(&E2,&FB,1):PROCsaa505xr(&F4,&FC,1):PROCsaa505xr(&FB,&FD,1):PROCsaa505xr(&E7,&FE,1): REM Belgian
  180WHEN 5,5055:PROCsaa505xr(&23,&A3,1):FOR loop%=&5B TO &60:PROCsaa505xr(loop%, loop%+128,1):NEXT:PROCsaa505xr(&7B,&FB,1):PROCsaa505xr(&A6,&7C,1):PROCsaa505xr(&7D,&FD,1):PROCsaa505xr(&7E,&FE,1): REM US-ASCII
  190WHEN 6,5056:FOR loop%=0 TO 27:PROCsaa505xr(loop%,loop%+224,1):NEXT: REM Hebrew
  200WHEN 7,5057:PROCsaa505xr(&23,&A3,1):PROCsaa505xr(&3F,&A6,2):FOR loop%=0 TO &3E:PROCsaa505xr(loop%,loop%+&C0,2):NEXT:REM Cyrillic
  210ENDCASE
  220SYS "OS_Byte",42,2
  230ENDPROC
  240DEFPROCsaa505xr(chi%, cho%, bank%)
  250LOCAL blk%%
  260SYS"Brandy_MAlloc",64 TO blk%%
  270blk%%?0 = 4
  280blk%%?1 = 44
  290blk%%?2 = chi%
  300blk%%?3 = bank%
  310SYS "OS_Word",&8B,blk%%
  320blk%%?0 = 44
  330blk%%?1 = 0
  340blk%%?2 = cho%
  350blk%%?3 = 0
  360SYS "OS_Word",&8C,blk%%
  370SYS"Brandy_Free",blk%%
  380ENDPROC
